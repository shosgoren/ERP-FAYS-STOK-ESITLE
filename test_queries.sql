-- Test Sorguları - Azure Data Studio'da çalıştırın
-- LOGO ve FAYS Stok Karşılaştırması

-- 1. LOGO Stokları Kontrol
USE GOLD;
GO

SELECT 
    ITEMS.CODE AS [MALZEME KODU],
    RTRIM(LTRIM(ITEMS.NAME)) AS [MALZEME ADI],
    ITEMS.STGRPCODE AS [GRUP KODU],
    AMBARLAR.NAME AS [AMBAR ADI],
    ROUND(SUM(ST.ONHAND),2) AS [LOGO FİİLİ STOK]
FROM LG_013_ITEMS AS ITEMS WITH (NOLOCK)
INNER JOIN LV_013_01_STINVTOT AS ST WITH (NOLOCK) ON ST.STOCKREF = ITEMS.LOGICALREF 
LEFT JOIN L_CAPIWHOUSE AS AMBARLAR WITH (NOLOCK) ON AMBARLAR.NR = ST.INVENNO AND AMBARLAR.FIRMNR = '013'
WHERE ST.INVENNO <> -1 AND ITEMS.ACTIVE=0
GROUP BY ITEMS.CODE, ITEMS.NAME, ITEMS.STGRPCODE, ST.INVENNO, AMBARLAR.NAME
ORDER BY ITEMS.CODE;
GO

-- 2. FAYS Stokları Kontrol
USE FaysWMSAkturk;
GO

SELECT 
    RTRIM(LTRIM(LN.StokKodu)) AS [MALZEME KODU],
    RTRIM(LTRIM(LN.UrunGrup1)) AS [MALZEME ADI],
    RTRIM(LTRIM(LN.Depo)) AS [DEPO],
    SUM(CASE WHEN FS.GirisCikis=2 THEN (-1)*LN.NetMiktar ELSE LN.NetMiktar END) AS [FAYS STOK]
FROM stk_Fis AS FS WITH (NOLOCK)
INNER JOIN stk_FisLines AS LN WITH (NOLOCK) ON LN.Link_FisNo = FS.FisNo
GROUP BY LN.StokKodu, LN.UrunGrup1, LN.Depo
HAVING SUM(CASE WHEN FS.GirisCikis=2 THEN (-1)*LN.NetMiktar ELSE LN.NetMiktar END) <> 0
ORDER BY LN.StokKodu;
GO

-- 3. KARŞILAŞTIRMA (Ana Sorgu)
USE FaysWMSAkturk;
GO

SELECT
    X.[MALZEME KODU],
    X.[MALZEME ADI],
    X.[GRUP KODU],
    X.[AMBAR ADI],
    ROUND(ISNULL(SUM(X.[FİİLİ STOK]),0),2) AS [LOGO FİİLİ STOK],
    ROUND(ISNULL(SUM(X.[FAYS STOK]),0),2) AS [FAYS STOK],
    ROUND(ISNULL(SUM(X.[FAYS STOK]),0),2)-ROUND(ISNULL(SUM(X.[FİİLİ STOK]),0),2) AS [FARK]
FROM
(
    SELECT     
        [AMBAR ADI] = AMBARLAR.NAME, 
        ITEMS.CODE AS [MALZEME KODU], 
        RTRIM(LTRIM(ITEMS.NAME)) AS [MALZEME ADI], 
        ISNULL(ITEMS.STGRPCODE,'') AS [GRUP KODU],
        ROUND(SUM(ST.ONHAND),2) AS [FİİLİ STOK],
        ROUND((SUM(ST.ONHAND) - SUM(ST.RESERVED) + SUM(ST.TEMPOUT) - SUM(ST.TEMPIN)),2) AS [GERÇEK STOK],
        ROUND(SUM(ST.ONHAND)-SUM(ST.RESERVED),2) AS [SEVKEDİLEBİLİR STOK], 
        0 AS [FAYS STOK]
    FROM         
        GOLD..LG_013_ITEMS AS ITEMS WITH (NOLOCK)		 
        INNER JOIN GOLD..LV_013_01_STINVTOT AS ST WITH (NOLOCK) ON ST.STOCKREF = ITEMS.LOGICALREF 
        LEFT JOIN GOLD..L_CAPIWHOUSE AS AMBARLAR WITH (NOLOCK) ON AMBARLAR.NR = ST.INVENNO AND AMBARLAR.FIRMNR = '013' 
    WHERE ST.INVENNO <> -1 AND ITEMS.ACTIVE=0
    GROUP BY ITEMS.CODE, ITEMS.NAME, ITEMS.STGRPCODE, ST.INVENNO, AMBARLAR.NAME
    
    UNION ALL
    
    SELECT    
        CAST(A.DEPO AS VARCHAR(50)) COLLATE Turkish_CI_AS AS [AMBAR ADI],
        CAST(A.StokKodu AS VARCHAR(50)) COLLATE Turkish_CI_AS AS MALZEME_KODU, 
        RTRIM(LTRIM(CAST(A.STOK_ADI AS VARCHAR(50)) COLLATE Turkish_CI_AS)) AS MALZEME_ADI,
        A.[GRUP KODU] AS [GRUP KODU],
        0 AS [FİİLİ STOK],
        0 AS [GERÇEK STOK],
        0 AS [SEVKEDİLEBİLİR STOK],
        ROUND(ISNULL(SUM(A.GIRIS_TOPLAM),0),2) - ROUND(ISNULL(SUM(A.CIKIS_TOPLAM),0),2) AS FAYS_MIKTAR
    FROM         
    (
        SELECT     
            FL.Depo AS DEPO,
            FL.StokKodu, 
            FL.UrunGrup1 AS STOK_ADI,
            I.STGRPCODE AS [GRUP KODU],
            CASE F.GirisCikis 
                WHEN 1 THEN ROUND(ISNULL(SUM(FL.NetMiktar),0),2) 
                ELSE 0 
            END AS GIRIS_TOPLAM,
            CASE F.GirisCikis 
                WHEN 2 THEN ROUND(ISNULL(SUM(FL.NetMiktar),0),2) 
                ELSE 0 
            END AS CIKIS_TOPLAM
        FROM          
            dbo.stk_Fis AS F WITH (NOLOCK)  
            INNER JOIN stk_FisLines AS FL WITH (NOLOCK) ON F.FisNo = FL.Link_FisNo 
            LEFT OUTER JOIN GOLD..LG_013_ITEMS AS I ON I.CODE=FL.StokKodu COLLATE Turkish_CI_AS
        GROUP BY FL.Depo, FL.StokKodu, FL.UrunGrup1, I.STGRPCODE, F.GirisCikis
    ) AS A
    GROUP BY A.DEPO, A.STOKKODU, A.STOK_ADI, A.[GRUP KODU]
) X
GROUP BY X.[MALZEME KODU], X.[MALZEME ADI], X.[GRUP KODU], X.[AMBAR ADI]
HAVING ROUND(ISNULL(SUM(X.[FAYS STOK]),0),2)-ROUND(ISNULL(SUM(X.[FİİLİ STOK]),0),2) <> 0
ORDER BY X.[MALZEME KODU];
GO

-- BEKLENENç SONUÇLAR:
-- 61007030: FAYS=120, LOGO=100, FARK=+20 (FAYS FAZLA)
-- 343403022: FAYS=60, LOGO=75, FARK=-15 (FAYS EKSİK)
-- TEST001: FAYS=180, LOGO=200, FARK=-20 (FAYS EKSİK)
-- TEST002: FAYS=30, LOGO=0, FARK=+30 (FAYS FAZLA)
-- 509V0004: Görünmemeli (fark yok)

